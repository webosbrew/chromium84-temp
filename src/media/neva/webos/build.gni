# Copyright 2018-2020 LG Electronics, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0

import("//neva/neva.gni")

media_player_neva_webos = {
  configs = []
  public_configs = []
  deps = []
  public_deps = [ "//base" ]
  _dir = get_path_info("../webos", "abspath")
  sources = [
    "$_dir/media_player_neva_factory_webos.cc",
    "$_dir/media_player_ums.cc",
    "$_dir/media_player_ums.h",
    "$_dir/webos_mediaclient.h",
    "$_dir/webos_mediaclient_stub.cc",
    "$_dir/webos_mediaclient_stub.h",
  ]

  use_umediaclient = false
  support_extinput = false
  support_photo = false
  support_camera = false
  use_luna_service = false

  if (webos_product_type == "tv") {
    use_umediaclient = true
    support_extinput = true
    support_photo = true
  }

  if (webos_product_type == "signage") {
    use_umediaclient = true
    support_extinput = true
    support_camera = true
    support_photo = true
  }

  if (use_umediaclient) {
    sources -= [
      "$_dir/webos_mediaclient_stub.cc",
      "$_dir/webos_mediaclient_stub.h",
    ]
    sources += [
      "$_dir/media_util.cc",
      "$_dir/media_util.h",
      "$_dir/system_media_manager.cc",
      "$_dir/system_media_manager.h",
      "$_dir/umediaclient_impl.cc",
      "$_dir/umediaclient_impl.h",
    ]

    if (!use_gav) {
      sources -= [
        "$_dir/system_media_manager.cc",
      ]
      if (webos_product_type == "tv") {
        sources += [
          "$_dir/system_media_manager_acb.cc",
          "$_dir/system_media_manager_acb.h",
          "$_dir/system_media_manager_tv.cc",
          "$_dir/system_media_manager_tv.h",
        ]
      } else if (webos_product_type == "signage") {
        sources += [
          "$_dir/system_media_manager_acb.cc",
          "$_dir/system_media_manager_acb.h",
          "$_dir/system_media_manager_signage.cc",
          "$_dir/system_media_manager_signage.h",
        ]
      }
    }
  }

  if (support_extinput) {
     sources += [
       "$_dir/media_player_extinput.cc",
       "$_dir/media_player_extinput.h",
     ]
    use_luna_service = true
   }

  if (support_photo) {
     sources += [
      "$_dir/media_player_photo.cc",
      "$_dir/media_player_photo.h",
     ]
    use_luna_service = true
  }

  if (support_camera) {
     sources += [
       "$_dir/media_player_camera.cc",
       "$_dir/media_player_camera.h",
     ]
   }

  if (use_luna_service) {
    deps += [ "//base/neva/webos" ]
  }
}

media_platform_api_webos = {
  configs = []
  public_configs = []
  deps = []
  public_deps = [ "//base" ]
  _dir = get_path_info("../webos", "abspath")
  sources = [ "$_dir/media_preferences_webos.cc" ]

  if (webos_product_type == "tv" || webos_product_type == "signage") {
    configs += [ "//media/neva/webos:media_base_webos_config" ]
    sources += [
      "$_dir/umedia_info_util_webos.cc",
      "$_dir/umedia_info_util_webos.h",
      "$_dir/umedia_info_util_webos_smp.cc",
      "$_dir/umedia_info_util_webos_smp.h",
      "$_dir/media_platform_api_webos_smp.cc",
      "$_dir/media_platform_api_webos_smp.h",
    ]
  }
  else {
    sources += [
      "$_dir/media_platform_api_webos_stub.cc",
      "$_dir/media_platform_api_webos_stub.h",
    ]
  }
  if (enable_lg_svp) {
    deps += [ "//media/neva/webos:svp_util" ]
  }
}
